
use crate::{Identifier, hir::{HirProgram, HirDeclaration, HirStructure, HirFunction, HirExpression, HirConstant, HirStatement, HirType}};

grammar;

pub Program: HirProgram = <(Declaration)*> => HirProgram::new(<>, 512);

Declaration: HirDeclaration = {
    "#" "[" "include" "(" <Str> ")" "]" => HirDeclaration::Include(<>),
    "#" "[" "heap" "(" <Num> ")" "]" => HirDeclaration::HeapSize(<> as i32),
    "const" <name:Ident> "=" <expr:Constant> ";" => HirDeclaration::Constant(name, expr),
    <Function> => HirDeclaration::Function(<>),
    <Structure> => HirDeclaration::Structure(<>),
}

Str: String = <s:r#""(\\.|[^"])*""#> => String::from(&s[1..s.len()-1]).replace("\\\"", "\"").replace("\\n", "\n").replace("\\r", "\r").replace("\\t", "\t").replace("\\0", "\0");
Char: char = <s:r#"'(\\.|[^'])'"#> => s.replace("\\'", "'").replace("\\n", "\n").replace("\\r", "\r").replace("\\t", "\t").replace("\\0", "\0").chars().nth(1).unwrap() as char;

Num: f64 = {
    r"([0-9]+([.][0-9]*)?|[.][0-9]+)" => <>.to_string().parse::<f64>().unwrap(),
}

Ident: Identifier = {
    <head:(r"[a-zA-Z_][a-zA-Z0-9_]*" "::")*> <tail:r"[a-zA-Z_][a-zA-Z0-9_]*"> => {
        let mut result = String::new();
        for (a, b) in head {
            result += a;
            result += b;
        }
        result += tail;
        result
    }
    
}

List<Begin, T, Sep, End>: Vec<T> = {
    <first:Begin> <list: (<T> <Sep>)*> <end:T?> <last:End> => {
        match end {
            None => list.iter().map(|(v, s)| v.clone()).collect(),
            Some(val) => {
                let mut list: Vec<_> = list.iter().map(|(v, s)| v.clone()).collect();
                list.push(val);
                list
            }
        }
    }
}

Constant: HirConstant = <ConstantMathLow> => <>;

ConstantAtom: HirConstant = {
    <Ident> => HirConstant::Constant(<>),
    <Num> => HirConstant::Float(<>),
    <Char> => HirConstant::Character(<>),
    "-" <ConstantAtom> => HirConstant::Subtract(Box::new(HirConstant::Float(0.0)), Box::new(<>))
}

ConstantMathLow: HirConstant = {
    <l:ConstantMathHigh> "+" <r:ConstantMathHigh> => HirConstant::Add(Box::new(l), Box::new(r)),
    <l:ConstantMathHigh> "-" <r:ConstantMathHigh> => HirConstant::Subtract(Box::new(l), Box::new(r)),
    <ConstantMathHigh> => <>
}

ConstantMathHigh: HirConstant = {
    <l:ConstantAtom> "*" <r:ConstantAtom> => HirConstant::Multiply(Box::new(l), Box::new(r)),
    <l:ConstantAtom> "/" <r:ConstantAtom> => HirConstant::Divide(Box::new(l), Box::new(r)),
    <ConstantAtom> => <>
}

Function: HirFunction = {
    "fn" <name:Ident> <args:List<"(", (Ident ":" Type), ",", ")">> <body:Body> => HirFunction::new(name, args.iter().map(|(a, _, t)| (a.clone(), t.clone())).collect(), HirType::Void, body),
    "fn" <name:Ident> <args:List<"(", (Ident ":" Type), ",", ")">> "->" <return_type:Type> <body:Body> => HirFunction::new(name, args.iter().map(|(a, _, t)| (a.clone(), t.clone())).collect(), return_type, body),
}

Structure: HirStructure = "type" <name:Ident> "(" <size:Constant> ")" "{" <methods:Function*> "}" => HirStructure::new(name, size, methods);

Body: Vec<HirStatement> = "{" <head: Statement*> <tail: SmallStatement?> "}" => {
    let mut result = Vec::new();
    for stmt in head { result.push(stmt) }
    if let Some(stmt) = tail { result.push(stmt) }
    result
};

Type: HirType = {
    "&" <t:Type> => HirType::Pointer(Box::new(t)),
    "void" => HirType::Void,
    "num"  => HirType::Float,
    "char" => HirType::Character,
    <Ident> => HirType::Structure(<>)
}

Statement: HirStatement = {
    <BodyStatement> => <>,
    <SmallStatement> ";" => <>
}

BodyStatement: HirStatement = {
    "for" "(" <pre:SmallStatement> ";" <cond:Expression> ";" <post:SmallStatement> ")" <body:Body> => HirStatement::For(Box::new(pre), cond, Box::new(post), body),
    "while" <cond:Expression> <body:Body> => HirStatement::While(cond, body),
    "if" <cond:Expression> <body:Body> => HirStatement::If(cond, body),
    "if" <cond:Expression> <then_body:Body> "else" <else_body:Body> => HirStatement::IfElse(cond, then_body, else_body),
}

SmallStatement: HirStatement = {
    "free" <addr:Expression> ":" <size:Expression> => HirStatement::Free(addr, size),
    "let" <name:Ident> "=" <expr:Expression> => HirStatement::AutoDefine(name, expr),
    "let" <name:Ident> ":" <t:Type> "=" <expr:Expression> => HirStatement::Define(name, t, expr),
    <name:Ident> "=" <expr:Expression> => HirStatement::AssignVariable(name, expr),
    "*" <lhs:Expression> "=" <rhs:Expression> => HirStatement::AssignAddress(lhs, rhs),
    <ptr:ExpressionAtom> "[" <idx:Expression> "]" "=" <rhs:Expression> => HirStatement::AssignAddress(HirExpression::Index(Box::new(ptr), Box::new(idx)), rhs),
    <instance:ExpressionAtom> "->" <name:Ident> "=" <rhs:Expression> => HirStatement::AssignAddress(HirExpression::Method(Box::new(instance), name, vec![]), rhs),

    <Expression> => HirStatement::Expression(<>)
}

Expression: HirExpression = {
    <ExpressionLow> => <>,
    "*" <ptr:ExpressionAtom> => HirExpression::Deref(Box::new(ptr)),
}

ExpressionAtom: HirExpression = {
    "alloc" "(" <size:Expression> ")" => HirExpression::Alloc(Box::new(size)),
    <name:Ident> <args:List<"(", Expression, ",", ")">> => HirExpression::Call(name, args),
    <name:Ident> "!" <args:List<"(", Expression, ",", ")">> => HirExpression::ForeignCall(name, args),
    <Ident> => HirExpression::Variable(<>),
    <Str> => HirExpression::String(<>),

    "@" => HirExpression::Void,
    <Num> => HirExpression::Constant(HirConstant::Float(<>)),
    <Char> => HirExpression::Constant(HirConstant::Character(<>)),

    "&" <name:Ident> => HirExpression::Refer(name),
    "(" <Expression> ")" => <>,
    "-" <ExpressionAtom> => HirExpression::Subtract(Box::new(HirExpression::Constant(HirConstant::Float(0.0))), Box::new(<>)),
}

ExpressionLow: HirExpression = {
    <l:ExpressionHigh> "+" <r:ExpressionHigh> => HirExpression::Add(Box::new(l), Box::new(r)),
    <l:ExpressionHigh> "-" <r:ExpressionHigh> => HirExpression::Subtract(Box::new(l), Box::new(r)),
    <ExpressionHigh> => <>
}

ExpressionHigh: HirExpression = {
    <ptr:ExpressionAtom> "[" <idx:Expression> "]" => HirExpression::Deref(Box::new(HirExpression::Index(Box::new(ptr), Box::new(idx)))),
    <instance:ExpressionAtom> "." <name:Ident> <args:List<"(", Expression, ",", ")">> => HirExpression::Method(Box::new(instance), name, args),
    <instance:ExpressionAtom> "." <name:Ident> => HirExpression::Method(Box::new(instance), name, vec![]),
    <instance:ExpressionAtom> "->" <name:Ident> <args:List<"(", Expression, ",", ")">> => HirExpression::Deref(Box::new(HirExpression::Method(Box::new(instance), name, args))),
    <instance:ExpressionAtom> "->" <name:Ident> => HirExpression::Deref(Box::new(HirExpression::Method(Box::new(instance), name, vec![]))),
    <l:ExpressionAtom> "*" <r:ExpressionAtom> => HirExpression::Multiply(Box::new(l), Box::new(r)),
    <l:ExpressionAtom> "/" <r:ExpressionAtom> => HirExpression::Divide(Box::new(l), Box::new(r)),
    <ExpressionAtom> => <>
}